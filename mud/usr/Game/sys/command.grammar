ordinal = /[1-9][0-9]*(,[0-9][0-9][0-9])*(st|nd|rd|th)/
count = /[1-9][0-9]*(,[0-9][0-9][0-9])*/

whisper_quote = /whisper .+/
say_quote = /say .+/
shout_quote = /shout .+/

word = /[a-z]+(-[a-z]+)*/
space = / +/
dummy = nomatch

COMMAND: 'inventory' ? parse_inventory_command
COMMAND: 'score' ? parse_score_command

COMMAND: 'go' _ DIRECTION ? parse_go_command
COMMAND: DIRECTION ? parse_go_command

COMMAND: 'look' ? parse_look_command
COMMAND: 'look' _ 'at' _ SELECTOR ? parse_look_at_command

COMMAND: 'take' _ SELECTOR ? parse_take_command
COMMAND: 'pick' _ 'up' _ SELECTOR ? parse_take_command
COMMAND: 'pick' _ SELECTOR _ 'up' ? parse_take_command
COMMAND: 'get' _ SELECTOR ? parse_take_command

COMMAND: 'drop' _ SELECTOR ? parse_drop_command
COMMAND: 'put' _ 'down' _ SELECTOR ? parse_drop_command
COMMAND: 'put' _ SELECTOR _ 'down' ? parse_drop_command
 
COMMAND: 'put' _ SELECTOR _ 'in' _ SELECTOR ? parse_put_in_command

COMMAND: 'take' _ SELECTOR _ 'out' _ 'of' _ SELECTOR ? parse_take_out_command
COMMAND: 'take' _ SELECTOR _ 'from' _ SELECTOR ? parse_take_out_command
COMMAND: 'get' _ SELECTOR _ 'out' _ 'of' _ SELECTOR ? parse_take_out_command
COMMAND: 'get' _ SELECTOR _ 'from' _ SELECTOR ? parse_take_out_command

COMMAND: 'wear' _ SELECTOR ? parse_wear_command
COMMAND: 'put' _ 'on' _ SELECTOR ? parse_wear_command
COMMAND: 'put' _ SELECTOR _ 'on' ? parse_wear_command

COMMAND: 'remove' _ SELECTOR ? parse_remove_command
COMMAND: 'take' _ 'off' _ SELECTOR ? parse_remove_command
COMMAND: 'take' _ SELECTOR _ 'off' ? parse_remove_command

COMMAND: 'wield' _ SELECTOR ? parse_wield_command
COMMAND: 'release' _ SELECTOR ? parse_release_command
COMMAND: 'attack' _ SELECTOR ? parse_attack_command

COMMAND: 'eat' _ SELECTOR ? parse_eat_command
COMMAND: 'drink' _ SELECTOR ? parse_drink_command
COMMAND: 'give' _ SELECTOR _ 'to' _ SELECTOR ? parse_give_command

COMMAND: WHISPER_QUOTE ? parse_whisper_command
COMMAND: SAY_QUOTE ? parse_say_command
COMMAND: SHOUT_QUOTE ? parse_shout_command

SELECTOR: LIST_SELECTOR

LIST_SELECTOR: _LIST_SELECTOR ? parse_list_selector
_LIST_SELECTOR: _LIST_SELECTOR ',' _ BASIC_SELECTOR
_LIST_SELECTOR: _LIST_SELECTOR _ 'and' _ BASIC_SELECTOR
_LIST_SELECTOR: _LIST_SELECTOR ',' _ 'and' _ BASIC_SELECTOR
_LIST_SELECTOR: BASIC_SELECTOR

BASIC_SELECTOR: SIMPLE_SELECTOR
BASIC_SELECTOR: ORDINAL_SELECTOR
BASIC_SELECTOR: COUNT_SELECTOR
BASIC_SELECTOR: ALL_OF_SELECTOR

SIMPLE_SELECTOR: PHRASE ? parse_simple_selector
SIMPLE_SELECTOR: 'the' _ PHRASE ? parse_simple_selector

ORDINAL_SELECTOR: _ORDINAL_SELECTOR ? parse_ordinal_selector
_ORDINAL_SELECTOR: ORDINAL _ PHRASE
_ORDINAL_SELECTOR: 'the' _ ORDINAL _ PHRASE
_ORDINAL_SELECTOR: ORDINAL _ 'of' _ PHRASE
_ORDINAL_SELECTOR: 'the' _ ORDINAL _ 'of' _ PHRASE
_ORDINAL_SELECTOR: ORDINAL _ 'of' _ 'the' _ PHRASE
_ORDINAL_SELECTOR: 'the' _ ORDINAL _ 'of' _ 'the' _ PHRASE

COUNT_SELECTOR: 'a' _ PHRASE ? parse_a_selector
COUNT_SELECTOR: 'an' _ PHRASE ? parse_a_selector
COUNT_SELECTOR: _COUNT_SELECTOR ? parse_count_selector
_COUNT_SELECTOR: COUNT _ PHRASE
_COUNT_SELECTOR: 'the' _ COUNT _ PHRASE
_COUNT_SELECTOR: COUNT _ 'of' _ PHRASE
_COUNT_SELECTOR: COUNT _ 'of' _ 'the' _ PHRASE

ALL_OF_SELECTOR: _ALL_OF_SELECTOR ? parse_all_of_selector
_ALL_OF_SELECTOR: 'all' _ PHRASE
_ALL_OF_SELECTOR: 'all' _ 'the' _ PHRASE
_ALL_OF_SELECTOR: 'all' _ 'of' _ PHRASE
_ALL_OF_SELECTOR: 'all' _ 'of' _ 'the' _ PHRASE
_ALL_OF_SELECTOR: 'each' _ PHRASE
_ALL_OF_SELECTOR: 'each' _ 'of' _ PHRASE
_ALL_OF_SELECTOR: 'each' _ 'of' _ 'the' _ PHRASE
_ALL_OF_SELECTOR: 'every' _ PHRASE

WHISPER_QUOTE: whisper_quote ? parse_quote
SAY_QUOTE: say_quote ? parse_quote
SHOUT_QUOTE: shout_quote ? parse_quote

PHRASE: _PHRASE ? parse_phrase
_PHRASE: _PHRASE word
_PHRASE: word

ORDINAL: _ORDINAL ? parse_ordinal
_ORDINAL: ordinal
_ORDINAL: 'first'
_ORDINAL: 'second'
_ORDINAL: 'third'
_ORDINAL: 'fourth'
_ORDINAL: 'fifth'
_ORDINAL: 'sixth'
_ORDINAL: 'seventh'
_ORDINAL: 'eighth'
_ORDINAL: 'ninth'
_ORDINAL: 'tenth'
_ORDINAL: 'eleventh'
_ORDINAL: 'twelveth'

COUNT: _COUNT ? parse_count
_COUNT: count
_COUNT: 'one'
_COUNT: 'two'
_COUNT: 'three'
_COUNT: 'four'
_COUNT: 'five'
_COUNT: 'six'
_COUNT: 'seven'
_COUNT: 'eight'
_COUNT: 'nine'
_COUNT: 'ten'
_COUNT: 'eleven'
_COUNT: 'twelve'

DIRECTION: 'down'
DIRECTION: 'east'
DIRECTION: 'north'
DIRECTION: 'northeast'
DIRECTION: 'northwest'
DIRECTION: 'south'
DIRECTION: 'southeast'
DIRECTION: 'southwest'
DIRECTION: 'up'
DIRECTION: 'west'

_: space ? parse_space
