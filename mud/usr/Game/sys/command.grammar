word = /[0-9a-z]+(-[0-9a-z]+)*/
quote = /'[^']+'?|"[^"]+"?/
whitespace = / +/

CMD: 'inventory' ? parse_inventory_command

CMD: 'look' ? parse_look_command
CMD: 'look' 'at' SEL ? parse_look_at_command
CMD: 'look' DIR ? parse_look_to_command
CMD: 'listen' ? parse_listen_command
CMD: 'listen' 'to' SEL ? parse_listen_to_command
CMD: 'smell' ? parse_smell_command
CMD: 'smell' SEL ? parse_smell_command
CMD: 'touch' SEL ? parse_touch_command
CMD: 'taste' SEL ? parse_taste_command

CMD: 'go' DIR ? parse_go_command
CMD: DIR ? parse_go_command

CMD: 'pick' 'up' SEL ? parse_take_command
CMD: 'pick' SEL 'up' ? parse_take_command
CMD: 'take' SEL ? parse_take_command
CMD: 'get' SEL ? parse_take_command
CMD: 'put' 'down' SEL ? parse_drop_command
CMD: 'put' SEL 'down' ? parse_drop_command
CMD: 'drop' SEL ? parse_drop_command
 
CMD: 'put' SEL 'in' SEL ? parse_put_in_command
CMD: 'take' SEL 'out' 'of' SEL ? parse_take_from_command
CMD: 'take' SEL 'from' SEL ? parse_take_from_command
CMD: 'get' SEL 'out' 'of' SEL ? parse_take_from_command
CMD: 'get' SEL 'from' SEL ? parse_take_from_command

CMD: 'put' 'on' SEL ? parse_wear_command
CMD: 'put' SEL 'on' ? parse_wear_command
CMD: 'wear' SEL ? parse_wear_command
CMD: 'take' 'off' SEL ? parse_remove_command
CMD: 'take' SEL 'off' ? parse_remove_command
CMD: 'remove' SEL ? parse_remove_command

CMD: 'wield' SEL ? parse_wield_command
CMD: 'release' ? parse_release_command

CMD: 'eat' SEL ? parse_eat_command
CMD: 'drink' SEL ? parse_drink_command

CMD: 'open' SEL ? parse_open_command
CMD: 'close' SEL ? parse_close_command

CMD: 'lock' SEL ? parse_lock_command
CMD: 'lock' SEL 'with' SEL ? parse_lock_command
CMD: 'unlock' SEL ? parse_unlock_command
CMD: 'unlock' SEL 'with' SEL ? parse_unlock_command

CMD: 'give' SEL 'to' SEL ? parse_give_command
CMD: 'attack' SEL ? parse_attack_command

CMD: 'read' SEL ? parse_read_command
CMD: 'write' QUOTE 'on' SEL ? parse_write_command

CMD: 'push' SEL ? parse_push_command
CMD: 'pull' SEL ? parse_pull_command

CMD: 'invite' SEL ? parse_invite_command
CMD: 'join' SEL ? parse_join_command
CMD: 'appoint' SEL ? parse_appoint_command
CMD: 'resign' ? parse_resign_command
CMD: 'dismiss' SEL ? parse_dismiss_command
CMD: 'disband' ? parse_disband_command

CMD: 'whisper' QUOTE ? parse_whisper_command
CMD: 'whisper' QUOTE 'to' SEL ? parse_whisper_command

CMD: 'say' QUOTE ? parse_say_command
CMD: 'say' QUOTE 'to' SEL ? parse_say_command
CMD: QUOTE ? parse_say_command
CMD: QUOTE 'to' SEL ? parse_say_command

CMD: 'shout' QUOTE ? parse_shout_command
CMD: 'shout' QUOTE 'to' SEL ? parse_shout_command

WORD: word ? parse_word
REC_WORD: WORD
REC_WORD: REC_WORD WORD
WORDS: REC_WORD ? parse_words

OPT_THE:
OPT_THE: 'the'
OPT_A:
OPT_A: 'a'

SEL_1: OPT_THE WORDS ? parse_simple_selector
SEL_1: OPT_THE ORD WORDS ? parse_ordinal_selector
SEL_1: OPT_THE ORD 'of' OPT_THE WORDS ? parse_ordinal_selector

SEL_1: 'a' WORDS ? parse_a_selector
SEL_1: 'an' WORDS ? parse_a_selector
SEL_1: COUNT WORDS ? parse_count_selector
SEL_1: COUNT 'of' OPT_THE WORDS ? parse_count_selector

SEL_1: 'all' ? parse_all_selector
SEL_1: 'all' OPT_THE WORDS ? parse_all_of_selector
SEL_1: 'all' 'of' OPT_THE WORDS ? parse_all_of_selector
SEL_1: 'each' WORDS ? parse_all_of_selector
SEL_1: 'each' 'of' OPT_THE WORDS ? parse_all_of_selector
SEL_1: 'every' WORDS ? parse_all_of_selector

REC_SEL_1: SEL_1
REC_SEL_1: REC_SEL_1 ',' SEL_1
REC_SEL_1: REC_SEL_1 'and' SEL_1
REC_SEL_1: REC_SEL_1 ',' 'and' SEL_1
REC_SEL_1: REC_SEL_1 'or' SEL_1
REC_SEL_1: REC_SEL_1 ',' 'or' SEL_1
SEL_2: REC_SEL_1 ? parse_list_selector

SEL_3: SEL_2
SEL_3: SEL_2 'except' SEL_2 ? parse_except_selector
SEL_3: SEL_2 'but' SEL_2 ? parse_except_selector
SEL_3: SEL_2 'but' 'not' SEL_2 ? parse_except_selector
SEL: SEL_3

COUNT: word ? parse_count
ORD: word ? parse_ordinal

DIR: word ? parse_direction
DIR: 'up' ? parse_direction
DIR: 'down' ? parse_direction

QUOTE: quote ? parse_quote
